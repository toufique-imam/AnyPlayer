plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlinx-serialization'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

android {
    compileSdk 32
    buildToolsVersion '32.1.0 rc1'

    defaultConfig {
        applicationId "com.stream.jmxplayer"
        minSdkVersion 21
        targetSdk 32
        versionCode 22
        versionName "2.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.incremental": "true"]
            }
        }
        def propertiesFile = rootProject.file('local.properties')
        def properties = new Properties()
        properties.load(new FileInputStream(propertiesFile))
        resValue 'string', 'app_sign', properties['APP_SIGN']
        resValue 'string', 'ADMOB_ID', properties['ADMOB_ID']
        resValue 'string', 'Interstitial_AD_ID', properties['Interstitial_AD_ID']
        resValue 'string', 'REWARD_AD_ID', properties['REWARD_AD_ID']
        resValue 'string', 'app_lvl', properties['APP_LVL']
        resValue 'string', 'app_lvl_paid', properties['APP_LVL_PAID']
    }
    dataBinding {
        enabled = true
    }

    flavorDimensions "version"

    productFlavors {
        free {
            dimension "version"
            applicationIdSuffix ".free"
            versionNameSuffix "-free"
            resValue "string", "app_name", "JMX Player"
        }
        paid {
            dimension "version"
            applicationIdSuffix ".paid"
            versionNameSuffix "-paid"
            resValue "string", "app_name", "JMX Player Pro"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true
        }
    }
}


dependencies {
    implementation files('libs/extension-ffmpeg-release.aar')
    implementation files('libs/extension-av1-release.aar')
    implementation files('libs/extension-vp9-release.aar')
    implementation files('libs/extension-flac-release.aar')
    implementation files('libs/extension-opus-release.aar')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation "androidx.activity:activity-ktx:1.4.0"
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    //exoplayer
    implementation 'com.google.android.exoplayer:exoplayer:2.17.1'

    //preference library
    implementation 'androidx.preference:preference-ktx:1.2.0'
    //for serving offline file for casting
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    //crash handler
    implementation 'cat.ereza:customactivityoncrash:2.3.0'
    //toast crash fix
    implementation 'me.drakeet.support:toastcompat:1.1.0'
    // image
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'
    //imageViewer
    implementation 'com.github.stfalcon-studio:StfalconImageViewer:v1.0.1'
    //animation
    implementation "com.airbnb.android:lottie:3.7.0"
    //admob
    implementation 'com.google.android.gms:play-services-ads:21.0.0'
    implementation 'com.google.android.gms:play-services-ads-identifier:18.0.1'
    implementation 'com.google.android.gms:play-services-location:20.0.0'
    //gson
    implementation 'com.google.code.gson:gson:2.8.9'
    //cast
    implementation 'androidx.mediarouter:mediarouter:1.3.0'
    implementation 'com.google.android.gms:play-services-cast-framework:21.0.1'
    //navigation component library
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    //room
    implementation "androidx.room:room-runtime:2.4.2"
    kapt "androidx.room:room-compiler:2.4.2"
    //kotlin coroutine support
    implementation "androidx.room:room-ktx:2.4.2"
    //piracyChecker
    implementation 'com.github.javiersantos:PiracyChecker:1.2.7'
    //volley
    implementation 'com.android.volley:volley:1.2.1'
    //libVLC
    implementation 'org.videolan.android:libvlc-all:3.4.4'
    //ijkPlayer
    implementation fileTree(include: ['*.aar'], dir: 'libs')
    //okkhttp
    implementation "com.squareup.okhttp3:okhttp:4.9.0"
}